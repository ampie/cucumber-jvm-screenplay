buildscript {
    repositories {
        maven {
            if (System.getProperty("runExternally") == 'true') {
                url "https://plugins.gradle.org/m2/"
            } else {
                url 'http://nexus.standardbank.co.za:8090/nexus/content/groups/public/'
            }
        }
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}
group='com.sbg.bdd'
version='0.1.4'
subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'maven-publish-auth'
    ext {
        CUKES_VERSION = '1.2.5'
        WIREMOCK_VERSION = '2.6.0'
        SCOPED_WIREMOCK_VERSION = '0.1.2'
    }
    repositories {
        mavenLocal()
        if (System.getProperty("runExternally") == 'true') {
            maven { url "http://repo.maven.apache.org/maven2" }
        } else {
            maven {
                url 'http://nexus.standardbank.co.za:8090/nexus/content/groups/public/'
            }
        }
    }
    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'org.codehaus.groovy:groovy-all:2.4.5'
        testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
            exclude group: "junit"
            exclude module: "groovy-all"
        }
        testCompile("com.github.goldin:spock-extensions:0.1.4") {
            exclude module: "spock-core"
            exclude module: "slf4j-api"
        }
    }
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    task packageSources(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    artifacts.archives packageSources

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }
    publishing {
        repositories {
            maven {
                name 'releases'
                url 'http://nexus.standardbank.co.za:8090/nexus/content/repositories/releases/'
            }
        }
        publications {
            mavenJava(MavenPublication) {

                println "Publishing $project.name:$project.group:$rootProject.version"
                groupId "$rootProject.group"
                artifactId "$project.name"
                version "$rootProject.version"

                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }

                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST
                        name "$rootProject.name"
                        description "$rootProject.description"
                    }
                    // Preserve compile-scope dependencies
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                            dep.name == it.artifactId.text()
                        }
                    }.each() {
                        it.scope*.value = 'compile'
                    }
                }
            }
        }
    }
}
